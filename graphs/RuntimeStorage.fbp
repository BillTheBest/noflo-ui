INPORT=LoadData.DB:DB
INPORT=Dispatch.IN:IN
INPORT=FindRuntimes.CONTEXT:CONTEXT
OUTPORT=MergeContext.OUT:CONTEXT
OUTPORT=ListenNetwork.PACKET:PACKET

# Route requests
'open' -> ROUTES Dispatch(routers/GroupRouter)
Dispatch ROUTE -> START Loading(ui/CreateLoadingContext)
Loading OUT -> IN MergeContextPreSubscribe(core/Merge)
Dispatch MISSED -> IN ShowErrors(core/Output)

# Send all projects to the UI
'' -> START CreateRuntimesCtx(objects/CreateObject)
CreateRuntimesCtx OUT -> CONTEXT SetRuntimes(ui/SetToContext)
'runtimes' -> KEY SetRuntimes
LoadData(ui/LoadRuntimeData) RUNTIMES -> VALUE SetRuntimes CONTEXT -> IN MergeContextPreSubscribe

# Add compatible runtimes to already-populated contexts
LoadData RUNTIMES -> RUNTIMES FindRuntimes(ui/FindRuntimes)
FindRuntimes CONTEXT -> CONTEXT HoldConnection(ui/HoldRuntimeConnection)

# Build runtime context
LoadData RUNTIMES -> RUNTIMES PopulateRuntimeData(ui/PopulateRuntimeData)
Dispatch OUT[0] -> IN PopulateRuntimeData
PopulateRuntimeData OUT -> CONTEXT HoldDirectConnection(ui/HoldRuntimeConnection)
HoldDirectConnection CONNECT -> CONTEXT ConnectDirectRuntime(ui/ConnectionToContext)
ConnectDirectRuntime RUNTIME -> RUNTIME HoldDirectConnection
ConnectDirectRuntime RUNTIME -> RUNTIME ListComponents(ui/ListComponents)
ListComponents OUT -> IN MergeContextPreSubscribe

# Direct connection graph handling
ConnectDirectRuntime CONTEXT -> CONTEXT GetRuntimeGraph(ui/GetRuntimeGraph)
GetRuntimeGraph READY -> IN MergeContextPreSubscribe
GetRuntimeGraph READY -> CONTEXT EnableDebug(ui/ContextToDebug)
GetRuntimeGraph MISSING -> CONTEXT Collect

# Connection handling, shared between contexts
HoldConnection CONTEXT -> CONTEXT Collect(ui/CollectRemoteNodes)
Collect RUNTIME -> RUNTIME GetSource(runtime/GetSource)
Collect COMPONENT -> NAME GetSource
GetSource SOURCE -> COMPONENT Collect
GetSource ERROR -> ERROR Collect
Collect CONTEXT -> IN MergeContextPreSubscribe
Collect ERROR -> ERROR ErrorToCtx(ui/ErrorToContext) OUT -> IN MergeContextPreSubscribe

# Send local data to runtime
Collect CONTEXT -> CONTEXT EnableDebug

# Connection handling
HoldConnection CONNECT -> CONTEXT ConnectRuntime(ui/ConnectionToContext)
ConnectRuntime RUNTIME -> RUNTIME HoldConnection
ConnectRuntime RUNTIME -> RUNTIME ListComponents(ui/ListComponents)
ListComponents OUT -> IN MergeContextPreSubscribe
ConnectRuntime CONTEXT -> CONTEXT Collect

# Graph change subscription
MergeContextPreSubscribe OUT -> CONTEXT SubscribeGraph(ui/SubscribeGraph)
SubscribeGraph RUNTIME -> RUNTIME SendGraph(runtime/SendGraphChanges)
SubscribeGraph GRAPH -> GRAPH SendGraph
SubscribeGraph OUT -> IN MergeContext(core/Merge)

# Listen to runtime network events
EnableDebug CONTEXT -> CONTEXT SendContext(ui/ContextToRuntime)

SubscribeGraph RUNTIME -> RUNTIME ListenNetwork(runtime/ListenNetwork)
SubscribeGraph GRAPH -> GRAPH ListenNetwork
ListenNetwork PACKET -> PACKET PacketToContext(ui/PacketToContext)
PacketToContext OUT -> IN MergeContext

# Listen to runtime process error events
SubscribeGraph RUNTIME -> RUNTIME ListenProcessErrors(runtime/ListenProcessErrors)
ListenProcessErrors PROCESS -> PROCESS ProcessErrorToContext(ui/ProcessErrorToContext)
ListenProcessErrors MESSAGE -> MESSAGE ProcessErrorToContext
ProcessErrorToContext OUT -> IN MergeContext


LoadData ERROR -> IN ShowErrors
