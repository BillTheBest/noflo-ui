INPORT=Dispatch.IN:IN
INPORT=LoadUser.START:START
OUTPORT=Merge.OUT:USER
OUTPORT=MergeContext.OUT:CONTEXT

'loginFree,loginPro,logout' -> ROUTES Dispatch(routers/GroupRouter)

# Initial persistent user data
LoadUser(ui/LoadUserData) USER -> IN Merge(core/Merge)
# In case of errors reset
LoadUser ERROR -> CLEAR Reset(ui/ClearUserData)

# Login handling
'$NOFLO_OAUTH_PROVIDER' -> SITE Login(ui/RemoteLogin)
'$NOFLO_OAUTH_SERVICE_USER' -> SITE GetRemoteUser(ui/GetRemoteUser)
'$NOFLO_OAUTH_GATE' -> GATEKEEPER Login
'$NOFLO_OAUTH_CLIENT_ID' -> CLIENTID Login
'github' -> SCOPE Login
Dispatch OUT[0] -> LOGIN Login
Login TOKEN -> TOKEN GetRemoteUser
GetRemoteUser USER -> USER StoreUser(ui/StoreUser)
Login ERROR -> ERROR ErrorToCtx(ui/ErrorToContext) OUT -> IN MergeContext(core/Merge)
GetRemoteUser ERROR -> ERROR ErrorToCtx
StoreUser USER -> IN Merge


# On browser we need to check login state immediately
'' -> CHECK Login

# Re-login handling for Pro
'$NOFLO_OAUTH_PROVIDER' -> SITE ProLogin(ui/RemoteLogin)
'$NOFLO_OAUTH_SERVICE_USER' -> SITE ProGetRemoteUser(ui/GetRemoteUser)
'$NOFLO_OAUTH_GATE' -> GATEKEEPER ProLogin
'$NOFLO_OAUTH_CLIENT_ID' -> CLIENTID ProLogin
'github_private' -> SCOPE ProLogin
Dispatch OUT[1] -> LOGIN ProLogin
ProLogin TOKEN -> TOKEN ProGetRemoteUser
ProGetRemoteUser USER -> USER StoreUser
ProLogin ERROR -> ERROR ErrorToCtx
ProGetRemoteUser ERROR -> ERROR ErrorToCtx

# Logout handling
Dispatch OUT[2] -> CLEAR Reset
Reset USER -> IN Merge
