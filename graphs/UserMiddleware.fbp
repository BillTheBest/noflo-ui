INPORT=Dispatch.IN:ACTION
OUTPORT=Passed.OUT:PASS
OUTPORT=NewActions.OUT:ACTION

'application:start,user:login,user:logout' -> ROUTES Dispatch(ui/DispatchAction)

# Pass actions we don't care about onwards
Dispatch PASS -> IN Passed(core/Merge)

# New actions generated by this middleware
'user:info' -> ACTION UserInfoAction(ui/SetAction)
UserInfoAction OUT -> IN NewActions(core/Merge)
'user:error' -> ACTION UserErrorAction(ui/SetAction)
UserErrorAction OUT -> IN NewActions
'application:redirect' -> ACTION ApplicationRedirectAction(ui/SetAction)
ApplicationRedirectAction OUT -> IN NewActions

# Start action handling
# - Check if we have an authorization code in URL and exchange for token
# - Load user from localStorage
Dispatch HANDLE[0] -> IN CheckQuery(ui/CheckTokenGrant)
# If we have an OAuth grant code, we need to try exchanging it
CheckQuery CODE -> CODE ExchangeToken(ui/ExchangeGrantToken)
CheckQuery ERROR -> IN UserErrorAction
ExchangeToken TOKEN -> TOKEN GetRemoteUser(ui/GetRemoteUser)
ExchangeToken ERROR -> IN UserErrorAction
GetRemoteUser USER -> USER StoreUser(ui/StoreUser)
GetRemoteUser ERROR -> IN UserErrorAction
StoreUser USER -> IN UserInfoAction

# Pass the start event also onwards
CheckQuery PASS -> IN Passed
# No grant code in URL, try to load local user data
CheckQuery PASS -> START LoadUser(ui/LoadUserData)
LoadUser USER -> IN UserInfoAction
LoadUser ERROR -> IN UserErrorAction

# Login action handling
Dispatch HANDLE[1] -> IN Login(ui/LoginOAuth)
# With Chrome app we get a new URL to check for codes
Login CODEURL -> IN CheckQuery
# With webapp we perform a URL redirect
Login REDIRECT -> IN ApplicationRedirectAction
Login ERROR -> IN UserErrorAction

# Logout action handling
Dispatch HANDLE[2] -> CLEAR Reset(ui/ClearUserData)
Reset USER -> IN UserInfoAction
